plugins {
    id 'war'  
}
description = 'Beam authorization manager app'
group 'org.jlab'
version '3.0.0-alpha0'
ext.releaseDate = 'Feb 23 2022'
ext.resourceVersion = '33'

project.sourceCompatibility = JavaVersion.VERSION_11
project.targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

configurations {
    dockerBuild.extendsFrom testRuntimeOnly
}

dependencies {
    if (project.hasProperty("provided")) {
        providedCompile 'org.jsoup:jsoup:1.11.3',
                'org.jlab:smoothness-weblib:3.3.0',
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate:hibernate-core:5.4.2.Final',
                'org.jlab:jlog:5.0.0'
    } else {
        implementation 'org.jsoup:jsoup:1.11.3',
                'org.jlab:smoothness-weblib:3.3.0',
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate:hibernate-core:5.4.2.Final',
                'org.jlab:jlog:5.0.0'
    }

    providedCompile('antlr:antlr') {
        version {
            strictly "2.7.7"
        }
    }

    // No matter what the Java EE API is provided by the application server
    providedCompile 'javax:javaee-api:8.0'

    dockerBuild 'com.oracle.database.jdbc:ojdbc11:21.3.0.0'
}
compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
war {
    archiveFileName = 'bam.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@RESOURCE_VERSION@", resourceVersion)
        }
    }
}

task initOracleRunSetup {
    outputs.dir "$rootDir/run/oracle/oradata"
    outputs.upToDateWhen {
        file("$rootDir/run/oracle/oradata").exists()
    }
    doLast {
        mkdir "$rootDir/run/oracle/oradata"
    }
}

task initWildflyRunSetup(type: Copy) {
    from "$rootDir/docker/wildfly"
    into "$rootDir/run/wildfly"
    exclude("docker-entrypoint.sh")
}

task dockerRunSetup {
    dependsOn(initOracleRunSetup)
    dependsOn(initWildflyRunSetup)
}

tasks.war.dependsOn(dockerRunSetup)

tasks.war.doLast {
    copy {
        from war
        into "$rootDir/run/wildfly/standalone/deployments"
    }
}