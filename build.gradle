plugins {
    id 'war'  
}
description = 'Beam authorization manager app'
group 'org.jlab'
version '3.0.0'
ext.releaseDate = 'Oct 4 2022'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

repositories {
    mavenCentral()

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

dependencies {
    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation 'org.jlab:smoothness-weblib:3.9.2'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
                    'org.jlab:jlog:5.0.0'

    // Wildfly 26.1.2.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
                    'org.hibernate:hibernate-core:5.3.28.Final',
                    'org.jsoup:jsoup:1.14.2'
}

war {
    archiveFileName = 'bam.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
