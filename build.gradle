plugins {
    id 'war'  
}
description = 'Beam authorization manager app'
group 'org.jlab'
version '3.0.0-alpha1'
ext.releaseDate = 'March 21 2022'
ext.resourceVersion = '33'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}

repositories {
    mavenCentral()

    maven {
        url "https://s01.oss.sonatype.org/content/repositories/releases/"
    }
}

dependencies {
    if (project.hasProperty("provided")) {
        providedCompile 'org.jsoup:jsoup:1.11.3',
                'org.jlab:smoothness-weblib:3.5.0',
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate:hibernate-core:5.4.2.Final',
                'org.jlab:jlog:5.0.0'
    } else {
        implementation 'org.jsoup:jsoup:1.11.3',
                'org.jlab:smoothness-weblib:3.5.0',
                'org.tuckey:urlrewritefilter:4.0.4',
                'org.hibernate:hibernate-core:5.4.2.Final',
                'org.jlab:jlog:5.0.0'
    }

    providedCompile('antlr:antlr') {
        version {
            strictly "2.7.7"
        }
    }

    // No matter what the Java EE API is provided by the application server
    providedCompile 'javax:javaee-api:8.0'
}

war {
    archiveFileName = 'bam.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@RESOURCE_VERSION@", resourceVersion)
        }
    }
}
